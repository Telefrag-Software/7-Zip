cmake_minimum_required(VERSION 3.19 FATAL_ERROR)

set_property(GLOBAL PROPERTY USE_FOLDERS ON)
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_SOURCE_DIR}/CMake")

project(SevenZip VERSION 21.7.0 LANGUAGES C)

include(SourceFiles)

list(TRANSFORM SEVEN_ZIP_C_SOURCE_FILES PREPEND "C/")
set(SEVEN_ZIP_ALL_SOURCE_FILES ${SEVEN_ZIP_C_SOURCE_FILES})

set(SEVEN_ZIP_ALL_HEADER_FILES ${SEVEN_ZIP_ALL_SOURCE_FILES})
list(FILTER SEVEN_ZIP_ALL_HEADER_FILES INCLUDE REGEX ".*\\.h$")

add_library(${PROJECT_NAME} STATIC ${SEVEN_ZIP_ALL_SOURCE_FILES})

include(GNUInstallDirs)

set(CONFIG_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")
set(GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/generated")
set(VERSION_CONFIG "${GENERATED_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
set(PROJECT_CONFIG "${GENERATED_DIR}/${PROJECT_NAME}Config.cmake")
set(TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")

include(CMakePackageConfigHelpers)

write_basic_package_version_file(
	"${VERSION_CONFIG}" COMPATIBILITY SameMajorVersion
)

configure_package_config_file(
	"cmake/Config.cmake.in"
	"${PROJECT_CONFIG}"
	INSTALL_DESTINATION "${CONFIG_INSTALL_DIR}"
)

install(
	TARGETS ${PROJECT_NAME}
	EXPORT "${TARGETS_EXPORT_NAME}"
	LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
	ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
	RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
	INCLUDES DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}"
)

foreach(_HEADER_FILE ${SEVEN_ZIP_ALL_HEADER_FILES})
	string(REGEX MATCH ".*[/\\]" _HEADER_DIRECTORY_PATH ${_HEADER_FILE})
	install(FILES ${_HEADER_FILE} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}/${_HEADER_DIRECTORY_PATH})
endforeach()

install(
	FILES "${PROJECT_CONFIG}" "${VERSION_CONFIG}"
	DESTINATION "${CONFIG_INSTALL_DIR}"
)

install(
	EXPORT "${TARGETS_EXPORT_NAME}"
	NAMESPACE "${PROJECT_NAME}::"
	DESTINATION "${CONFIG_INSTALL_DIR}"
)
